plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'jacoco'
    id 'com.google.gms.google-services'
    id 'kotlin-parcelize'
}

android {

    // Personal keystore to store the signing certificate for the debug app,
    // so that we don't have to all add our own SHA-1 to firebase
    signingConfigs {
        debug {
            storeFile file('../BeGreen.keystore')
            storePassword 'android'
            keyAlias 'be_green_ks'
            keyPassword 'android'
        }
    }
    namespace 'com.github.sdp_begreen.begreen'
    compileSdk 33

    defaultConfig {
        applicationId "com.github.sdp_begreen.begreen"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'com.github.sdp_begreen.begreen.InstrumentationTestRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    testOptions{
        animationsDisabled = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.test:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-testing:2.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    implementation "androidx.fragment:fragment-ktx:1.5.7"
    debugImplementation "androidx.fragment:fragment-testing:1.5.7"
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    // Real time DB + storage + google authenticator (check the following link to add any other dependency:
    // https://firebase.google.com/docs/android/setup#available-libraries
    implementation platform('com.google.firebase:firebase-bom:32.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'

    // Bootcamp google auth
    // https://firebase.google.com/docs/auth/android/firebaseui?hl=fr
    // implementation 'com.firebaseui:firebase-ui-auth:7.2.0'

    // Google Sign-in. See link :
    // https://developers.google.com/identity/sign-in/android/start-integrating
    implementation 'com.google.android.gms:play-services-auth:20.5.0'

    // insert-koin
    implementation 'io.insert-koin:koin-core:3.4.0'
    implementation 'io.insert-koin:koin-android:3.4.0'
    // Koin Test features
    androidTestImplementation "io.insert-koin:koin-test:3.4.0"
    // Koin for JUnit 4
    androidTestImplementation "io.insert-koin:koin-test-junit4:3.4.0"

    // mockito
    testImplementation "org.mockito:mockito-core:5.2.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.1.0"
    androidTestImplementation 'org.mockito:mockito-android:5.2.0'
    androidTestImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'

    // Googlemaps
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.maps.android:android-maps-utils:0.6.2'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0-alpha03'

    // Camera X

    def camerax_version = "1.2.2"
    // The following line is optional, as the core library is included indirectly by camera-camera2
    implementation("androidx.camera:camera-core:${camerax_version}")
    implementation("androidx.camera:camera-camera2:${camerax_version}")
    // If you want to additionally use the CameraX Lifecycle library
    implementation("androidx.camera:camera-lifecycle:${camerax_version}")
    // If you want to additionally use the CameraX View class
    implementation("androidx.camera:camera-view:${camerax_version}")

    // Picasso
    implementation("com.squareup.picasso:picasso:2.8")

    // Country picker
    implementation("com.hbb20:android-country-picker:0.0.7")

    implementation 'com.google.android.material:material:1.9.0'

    // GSON
    implementation 'com.google.code.gson:gson:2.10.1'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
    ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: project.buildDir, includes: [
            'outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec',
            'outputs/code_coverage/debugAndroidTest/connected/*/coverage.ec'
    ]))
}

connectedCheck {
    finalizedBy jacocoTestReport
}